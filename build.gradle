plugins {
    id 'java'

    id 'idea'
    id 'eclipse'

    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
defaultTasks 'clean', 'licenseFormat'

group 'uk.jamierocks'
version '1.0-SNAPSHOT'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:18.0'
    implementation 'com.beust:jcommander:1.48'
}

ext {
    DIR_RESOURCES = "$project.projectDir\\src\\main\\resources"
}

processResources {
    from 'LICENSE.txt'
}

jar {
    classifier = 'base'

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'uk.jamierocks.rgs2srg.Main'
        )
    }
}

shadowJar {
    classifier = ''
}

license {
    header rootProject.file('HEADER.txt')
    include '**/*.java'
    include '**/*.groovy'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

artifacts {
    archives shadowJar
}

task testConversion(type: JavaExec) {
    main = "uk.jamierocks.rgs2srg.Main"
    classpath = sourceSets.main.runtimeClasspath
    args("-i", "${DIR_RESOURCES}\\minecraft.rgs", "-o", "${DIR_RESOURCES}\\a1.2.6.srg")
}